set terminal epslatex clip size 6.5in, 3.3in;

set output 'figures/code_coverage_graph.tex'; 

set style data histogram
set style histogram cluster gap 2

set xrange [-.5:7.5]
set format y "%g\\%%";
set xzeroaxis -1

set style fill pattern

unset key
bm = 0.15
lm = 0.12
rm = 0.95
gap = 0.03
size = 0.75
y1 = -20.0; y2 = 122.0; y3 = 187.0; y4 = 250.0

set multiplot
set border 1+2+8
set xtics nomirror rotate by -30;
set ytics nomirror
set lmargin at screen lm
set rmargin at screen rm
set bmargin at screen bm
set tmargin at screen bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) )

set yrange [y1:y2]
plot 'data/code_coverage.dat' using 2:xtic(1) title col, '' using 3:xtic(1) title col, ''using 4:xtic(1) title col

unset xtics
unset xlabel
set key left
set border 2+4+8
set bmargin at screen bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap
set tmargin at screen bm + size + gap
set yrange [y3:y4]
set ytics nomirror
set ytics 190,20,250

set label 'Percentage Improvement Over \wget{}' at screen 0.03, bm + 0.5 * (size + gap) offset 0,-strlen("Percentage Improvement Over \wget{}")/4.0 rotate by 90

set arrow from screen lm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0 to screen \
lm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 nohead

set arrow from screen lm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0  + gap to screen \
lm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 + gap nohead

set arrow from screen rm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0 to screen \
rm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 nohead

set arrow from screen rm - gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1)+abs(y4-y3) ) ) - gap / 4.0  + gap to screen \
rm + gap / 4.0, bm + size * (abs(y2-y1) / (abs(y2-y1) + abs(y4-y3) ) ) + gap / 4.0 + gap nohead


plot 'data/code_coverage.dat' using ($2 > y3 ? $2 : 1/0) title col, '' using ($3 > y3 ? $3 : 1/0) title col, '' using ($4 > y3 ? $4 : 1/0):xtic(1) title col

unset multiplot